pragma cashscript ^0.8.0;


contract Vault(
    pubkey merchantReceiverPK,
    pubkey merchantSignerPK
) {

    function refund (sig merchantSignerSig) {
        require(checkSig(merchantSignerSig, merchantSignerPK));

        // 2 inputs: lock nft & fee funder utxo
        // 1 output: satoshis refund reward from burned lock nft
        require(tx.inputs.length >= 2);
        require(tx.outputs.length >= 1);

        // sent amount must be equal to the commitment data
        bytes claimExpiryTimestampBytes, bytes claimAmountBytes = tx.inputs[0].nftCommitment.split(20);
        int claimAmount = int(claimAmountBytes);
        int claimExpiryTimestamp = int(claimExpiryTimestampBytes);
        require(tx.outputs[0].value == claimAmount);

        // lock nft must be expired
        require(tx.time >= claimExpiryTimestamp);

        // claim recipient must be quest owner embedded inside nft commitment
        bytes20 merchantPKH = hash160(merchantReceiverPK);
        bytes25 merchant = new LockingBytecodeP2PKH(merchantPKH);
        require(tx.outputs[0].lockingBytecode == merchant);
    }

    function claim(
        bytes32 keyNftCategory,
        sig merchantSignerSig
    ) {
        require(checkSig(merchantSignerSig, merchantSignerPK));

        // 2 outputs: lock & key nft
        // 1 output: recipient of BCH stored in lock NFT
        require(tx.inputs.length >= 2);
        require(tx.outputs.length >= 1);

        // key NFT must be an immutable NFT
        require(tx.inputs[1].tokenCategory == keyNftCategory);

        // lock & key nft must have the same commitment
        // 20 bytes - claim expiration timestamp
        // 20 bytes - claim amount
        require(tx.inputs[0].nftCommitment == tx.inputs[1].nftCommitment);

        // sent amount must be equal to the commitment data
        bytes claimAmountBytes = tx.inputs[1].nftCommitment.split(20)[1];
        int claimAmount = int(claimAmountBytes);
        require(tx.outputs[0].value == claimAmount);

        // the amount sent must be from lock nft's input
        require(tx.inputs[0].value == claimAmount);

        // the funds must be sent to the merchant receiving address
        bytes20 merchantPKH = hash160(merchantReceiverPK);
        bytes25 merchant = new LockingBytecodeP2PKH(merchantPKH);
        require(tx.outputs[0].lockingBytecode == merchant);
    }

}
